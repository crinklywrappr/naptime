<S> = token (<','> token)*
<token> = (table-column|column|table)
<column> = (wildcard|regular-column|renamed-column|typed-column|json-column)
wildcard = <'*'>
table-column = (table-name <'.'>)* column
regular-column = #'[a-zA-Z0-9_]+'
renamed-column = regular-column <':'> regular-column
typed-column = regular-column <'::'> type
<type> = (regular-type|complex-type)
regular-type = #'[a-zA-Z0-9_ ]+'
complex-type = regular-type <'('> number (<','> number)* <')'>
number = #'[0-9]+'
json-column = (regular-column (json-nth|json-key))+ column
json-key = <'->>'>
json-nth = <'->'>
<table> = (table-columns|table-alias|table-hint)
table-columns = table-name <bang-inner?> <'('> S <')'>
table-name = #'[a-zA-Z0-9_]+'
table-alias = table-name <':'> table-columns
bang-inner = '!inner'
table-hint = table-name <'!'> (table-columns|table-alias)